
&НаКлиенте
Процедура СделатьЗвуковоеОповещениеОсновным(Команда)
	
	ТекущаяЗапись = Элементы.Список.ТекущиеДанные; 
	Если ТекущаяЗапись <> Неопределено Тогда 
		
		СделатьЗвуковоеОповещениеОсновнымНаСервере(ТекущаяЗапись.Ссылка); 
		Элементы.Список.Обновить();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СделатьЗвуковоеОповещениеОсновнымНаСервере(СсылкаОсновной)

	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ
	               |	СДЧ_ФайлыДляВоспроизведения.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.СДЧ_ФайлыДляВоспроизведения КАК СДЧ_ФайлыДляВоспроизведения
	               |ГДЕ
	               |	СДЧ_ФайлыДляВоспроизведения.ОсновноеСообщение
	               |	И НЕ СДЧ_ФайлыДляВоспроизведения.Ссылка = &СсылкаКУстановке"; 
	
    Запрос.УстановитьПараметр("СсылкаКУстановке", СсылкаОсновной); 
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	Пока Выборка.Следующий() Цикл 

		ОбъектОсновнаяЗапись = Выборка.Ссылка.ПолучитьОбъект(); 
		ОбъектОсновнаяЗапись.ОсновноеСообщение = Ложь; 
		ОбъектОсновнаяЗапись.Записать(); 
		
	КонецЦикла; 
	
	ОбъектОсновнаяЗапись = СсылкаОсновной.ПолучитьОбъект(); 
	Если НЕ ОбъектОсновнаяЗапись.ОсновноеСообщение Тогда 
		
		ОбъектОсновнаяЗапись.ОсновноеСообщение = Истина; 
		ОбъектОсновнаяЗапись.Записать(); 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Воспроизвести(Команда)
	
	ТекущаяЗапись = Элементы.Список.ТекущиеДанные; 
	Если ТекущаяЗапись <> Неопределено Тогда 
		СтруктураЗвук = СДЧ_ЗвуковыеОповещенияКлиент.ПроигратьЗвукОплаты(ТекущаяЗапись.Ссылка);
	Иначе 	
		СтруктураЗвук = СДЧ_ЗвуковыеОповещенияКлиент.ПроигратьЗвукОплаты();
	КонецЕсли;
	
	Если НЕ СтруктураЗвук.ЗвукПроигран Тогда 
		СтруктураЗвук.Вставить("Форма", ЭтаФорма); 
		СДЧ_ЗвуковыеОповещенияКлиент.ПроигратьЗвукНаФорме(СтруктураЗвук);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлСообщения(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл звукового оповещения";
	Диалог.ПолноеИмяФайла = ""; 
	Фильтр = "mp3 (*.mp3)|*.mp3"; 
	Диалог.Фильтр = Фильтр; 
	Диалог.МножественныйВыбор = Ложь;	
	Диалог.Показать(Новый ОписаниеОповещения("ВыбратьГолосовоеСообщениеКлиентЗавершение", ЭтаФорма, Новый Структура));		
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьГолосовоеСообщениеКлиентЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда				
		
		СодержимоеФайла = Новый Структура; 
		ФайлКЗагрузке =  Новый Файл(ВыбранныеФайлы[0]); 
		СодержимоеФайла.Вставить("АдресВХ",ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ФайлКЗагрузке.ПолноеИмя)));
		СодержимоеФайла.Вставить("ИмяФайла", ФайлКЗагрузке.ИмяБезРасширения); 
		СодержимоеФайла.Вставить("ФайлУжеЕсть", Ложь); 
		СодержимоеФайла.Вставить("КодСправочникаСуществует", "");
		ЗагрузитьГолосовоеСообщениеНаСервере(СодержимоеФайла);
		Если СодержимоеФайла.ФайлУжеЕсть Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Файл уже загружен, код: " 
				+ СодержимоеФайла.КодСправочникаСуществует); 
		КонецЕсли; 
	КонецЕсли; 
	Элементы.Список.Обновить();
	
КонецПроцедуры 

&НаСервере 
Процедура ЗагрузитьГолосовоеСообщениеНаСервере(СодержимоеФайла)

	Сообщение = ""; 
	ДД = ПолучитьИзВременногоХранилища(СодержимоеФайла.АдресВх); 
	УдалитьИзВременногоХранилища(СодержимоеФайла.АдресВх); 
	ИмяФайла = ПолучитьИмяВременногоФайла("mp3"); 
	ДД.Записать(ИмяФайла); 
	
	ХешСумма = СДЧ_ЗвуковыеОповещенияСервер.ХэшФайла(ИмяФайла); 
	УдалитьФайлы(ИмяФайла); 
	
	СсылкаЕсть = СДЧ_ЗвуковыеОповещенияСервер.ВернутьСсылкуПоХеш(ХешСумма); 
	
	Если СсылкаЕсть = Неопределено Тогда 
	
		ОбъектОповещение = Справочники.СДЧ_ФайлыДляВоспроизведения.СоздатьЭлемент(); 
		ОбъектОповещение.Наименование = СодержимоеФайла.ИмяФайла; 
		ОбъектОповещение.Медиа = Новый ХранилищеЗначения(ДД); 
		ОбъектОповещение.Хэш = ХешСумма; 
		ОбъектОповещение.Акитвность = Истина; 
		ОбъектОповещение.Записать();                          
		
	Иначе 	
		СодержимоеФайла.Вставить("ФайлУжеЕсть", Истина); 
		СодержимоеФайла.Вставить("КодСправочникаСуществует", СсылкаЕсть.Код);
	КонецЕсли; 
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.ПроизвольноеВоспроизведение 	= Константы.СДЧ_ВоспроизводитьВПроизвольномПорядке.Получить(); 
	ЭтотОбъект.ВоспроизводитьЗвукОС 		= Константы.СДЧ_ВоспроизводитьЗвукОС.Получить(); 
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ПроизвольноеВоспроизведениеПриИзмененииНаСервере(ПроизвольноеВоспроизведение)
	
	Константы.СДЧ_ВоспроизводитьВПроизвольномПорядке.Установить(ПроизвольноеВоспроизведение); 
	
КонецПроцедуры


&НаКлиенте
Процедура ПроизвольноеВоспроизведениеПриИзменении(Элемент)
	
	ПроизвольноеВоспроизведениеПриИзмененииНаСервере(ПроизвольноеВоспроизведение);
	
КонецПроцедуры


&НаСервере
Функция ВоспроизвестиСлучайноеСообщениеНаСервере()
	
	Возврат СДЧ_ЗвуковыеОповещенияСервер.ВернутьПроизвольныйФайлОповещения(); 

КонецФункции


&НаКлиенте
Процедура ВоспроизвестиСлучайноеСообщение(Команда)
	
	СсылкаНаСлучайноеСообщение = ВоспроизвестиСлучайноеСообщениеНаСервере(); 
	СтруктураЗвук = СДЧ_ЗвуковыеОповещенияКлиент.ПроигратьЗвукОплаты(СсылкаНаСлучайноеСообщение);
	СДЧ_ЗвуковыеОповещенияКлиент.ПроигратьЗвукНаФорме(СтруктураЗвук); 
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ВоспроизводитьЗвукОСПриИзмененииНаСервере(ВоспроизводитьЗвукОС)
	
	Константы.СДЧ_ВоспроизводитьЗвукОС.Установить(ВоспроизводитьЗвукОС); 

КонецПроцедуры


&НаКлиенте
Процедура ВоспроизводитьЗвукОСПриИзменении(Элемент)
	
	ВоспроизводитьЗвукОСПриИзмененииНаСервере(ВоспроизводитьЗвукОС);
	
КонецПроцедуры

