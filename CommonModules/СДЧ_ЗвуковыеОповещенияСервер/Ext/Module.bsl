
Функция ВернутьСодержимоеФайлаДляВоспроизведения(Ссылка) Экспорт

	СтруктураВозврата = Новый Структура; 
	СтруктураВозврата.Вставить("Ссылка", Ссылка); 	
	СтруктураВозврата.Вставить("ИмяФайла", Ссылка.Наименование); 	
	СтруктураВозврата.Вставить("АдресВХ", ПоместитьВоВременноеХранилище(Ссылка.Медиа.Получить())); 	
	
	Возврат СтруктураВозврата; 

КонецФункции                            

Функция ПолучитьСсылкуНаФайлОповещения() Экспорт 
	
	УстановитьПривилегированныйРежим(Истина); 
	СсылкаНаФайлОповещения = Неопределено; 
	Если Константы.СДЧ_ВоспроизводитьВПроизвольномПорядке.Получить() Тогда 
		
		СсылкаНаФайлОповещения = ВернутьПроизвольныйФайлОповещения(); 
		
	Иначе
		
		СсылкаНаФайлОповещения = ВернутьОсновнойФайлОповещения(); 
		
	КонецЕсли; 
	
	Возврат СсылкаНаФайлОповещения; 
	
КонецФункции

Функция ВернутьОсновнойФайлОповещения() Экспорт 

	УстановитьПривилегированныйРежим(Истина); 
	СсылкаНаОповещение = Неопределено; 
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ
	               |	СДЧ_ФайлыДляВоспроизведения.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.СДЧ_ФайлыДляВоспроизведения КАК СДЧ_ФайлыДляВоспроизведения
	               |ГДЕ
	               |	СДЧ_ФайлыДляВоспроизведения.ОсновноеСообщение"; 
	
 	Выборка = Запрос.Выполнить().Выбрать(); 
	Если Выборка.Следующий() Тогда 
		
		СсылкаНаОповещение = Выборка.Ссылка; 
			
	КонецЕсли; 
	
	Возврат СсылкаНаОповещение; 
	
КонецФункции 

Функция ВернутьПроизвольныйФайлОповещения() Экспорт 

	УстановитьПривилегированныйРежим(Истина); 
	СсылкаНаОповещение = Неопределено; 
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ
	               |	СДЧ_ФайлыДляВоспроизведения.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.СДЧ_ФайлыДляВоспроизведения КАК СДЧ_ФайлыДляВоспроизведения
	               |ГДЕ
	               |	СДЧ_ФайлыДляВоспроизведения.Акитвность"; 
	
 	Выборка = Запрос.Выполнить().Выгрузить(); 
	КоличествоЗаписей = Выборка.Количество() - 1; 
	
	Если КоличествоЗаписей >= 0 Тогда 
		Генератор = Новый ГенераторСлучайныхЧисел(255); 	
		СлучайныйНомер = Генератор.СлучайноеЧисло(0, КоличествоЗаписей); 
		СсылкаНаОповещение = Выборка[СлучайныйНомер].Ссылка; 
	КонецЕсли; 
	
	Возврат СсылкаНаОповещение; 
	

КонецФункции

Функция ХэшФайла(ИмяФайла) Экспорт 

	Хеш = Новый ХешированиеДанных(ХешФункция.MD5); 
	Хеш.ДобавитьФайл(ИмяФайла); 
	
	Возврат Строка(Хеш.ХешСумма); 

КонецФункции   

Функция ВернутьСсылкуПоХеш(ХешСумма) Экспорт 
	
	СсылкаНаЗапись = Неопределено; 
	
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ
	               |	СДЧ_ФайлыДляВоспроизведения.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.СДЧ_ФайлыДляВоспроизведения КАК СДЧ_ФайлыДляВоспроизведения
	               |ГДЕ
	               |	СДЧ_ФайлыДляВоспроизведения.Хэш = &Хэш"; 
	Запрос.УстановитьПараметр("Хэш", ХешСумма); 
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Если Выборка.Следующий() Тогда 
		
		СсылкаНаЗапись = Выборка.Ссылка; 
		
	КонецЕсли; 
	
	Возврат СсылкаНаЗапись; 
	
КонецФункции   

Функция ВоспроизводитьЗвукОС() Экспорт 
	
	Возврат Константы.СДЧ_ВоспроизводитьЗвукОС.Получить(); 	
	
КонецФункции