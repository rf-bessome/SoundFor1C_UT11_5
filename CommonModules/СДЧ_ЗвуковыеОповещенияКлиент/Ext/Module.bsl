
Функция ПроигратьЗвукОплаты(СсылкаФайл = Неопределено) Экспорт 

	Результат = Новый Структура("ЗвукПроигран", Ложь); 	
	Если СсылкаФайл = Неопределено Тогда 
		СсылкаФайл = СДЧ_ЗвуковыеОповещенияСервер.ПолучитьСсылкуНаФайлОповещения(); 	
	КонецЕсли; 
	
	Результат.Вставить("ВоспроизводитьЗвукОС", СДЧ_ЗвуковыеОповещенияСервер.ВоспроизводитьЗвукОС()); 
	
	НаименованиеФайла = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(СсылкаФайл, "Наименование").Наименование + ".mp3"; 
	ФайлОповещения = Новый Файл(КаталогВременныхФайлов() + "\" + НаименованиеФайла); 
	Если НЕ ФайлОповещения.Существует() Тогда 
		ОписаниеФайлаОповещения = СДЧ_ЗвуковыеОповещенияСервер.ВернутьСодержимоеФайлаДляВоспроизведения(СсылкаФайл); 
		ДД = ПолучитьИзВременногоХранилища(ОписаниеФайлаОповещения.АдресВХ); 
		УдалитьИзВременногоХранилища(ОписаниеФайлаОповещения.АдресВХ); 
		ДД.Записать(ФайлОповещения.ПолноеИмя); 
	КонецЕсли; 
	
	КодВозврата = 0; 
	Если Результат.ВоспроизводитьЗвукОС Тогда 
		ЗапуститьПриложение(ФайлОповещения.ПолноеИмя,КаталогВременныхФайлов(), Ложь, КодВозврата); 
		//Результат.ЗвукПроигран = КодВозврата = 0; 
		Результат.ЗвукПроигран = Истина; 
	КонецЕсли; 
	Результат.Вставить("ФайлОповещения", ФайлОповещения.ПолноеИмя); 
	
	Возврат Результат; 
	
КонецФункции  

Процедура ПроигратьЗвукНаФорме(СтруктураЗвук) Экспорт
	
	Если НЕ СтруктураЗвук.ЗвукПроигран Тогда 

		АдресФайла = СтруктураЗвук.ФайлОповещения; 
		Если НЕ СтрНачинаетсяС(НРег(АдресФайла), "http") Тогда
			АдресФайла = СтрЗаменить(АдресФайла,"\","/");
			АдресФайла = СтрШаблон("file://%1", АдресФайла);
		КонецЕсли;
		
		КодЗапускаОповещения = СтрШаблон("
	  | <HTML>
	  | <BODY scroll='no'> Проигрываем звук покупателю
	  | <BGSOUND id='sound_think' balance=0 volume='-1000'></BGSOUND>
	  | <div id='id_%2'</div>
	  | <script>
	  | window.onload = new Audio('%1').play();
	  | </script>
	  | </BODY>
	  | </HTML>", АдресФайла, Новый УникальныйИдентификатор);
		//ОткрытьФорму("Обработка.СДЧ_ВоспроизведениеЗвука.Форма.Форма", 
		//	Новый Структура("ТекстHTML",КодЗапускаОповещения));
		ВоспроизведениеЗвука = СДЧ_ЗвуковыеОповещенияКлиентПовтИсп.ПолучитьФормуВоспроизведенияЗвука(); 
		ВоспроизведениеЗвука.ПолеHTML = КодЗапускаОповещения; 
		ВоспроизведениеЗвука.Активизировать(); 
		
	КонецЕсли; 
	
КонецПроцедуры 

Процедура ЗвуковоеОповещениеПробитияЧека() Экспорт 

	Результат = СДЧ_ЗвуковыеОповещенияКлиент.ПроигратьЗвукОплаты();
	Если НЕ Результат.ЗвукПроигран Тогда 
		СДЧ_ЗвуковыеОповещенияКлиент.ПроигратьЗвукНаФорме(Результат);
	КонецЕсли;

КонецПроцедуры 